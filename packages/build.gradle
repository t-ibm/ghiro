description = 'Integration Server packages'
/*
 * Build script configuration
 */
buildscript {
    repositories {
        ivy {
            name = 'sag-central'
            layout 'maven'
            url = 'http://bas.eur.ad.sag:8081/artifactory/build-snapshot'
        }
    }
    dependencies {
        classpath "com.softwareag.bas:bas-plugins-is:$project.sag.bas.version@jar"
        classpath group:'com.softwareag.mom.is', name:'is-server', version:project.sag.is.version, configuration:'nodeutil'
    }
}
/*
 * Project specific configuration
 */
task clean(type:Delete, group:'build') {
    description = 'Deletes the build directory.'
    delete project.buildDir
}
/*
 * Project domain specific configuration
 */
subprojects {
    group = "com.softwareag.mom.is"
    apply from: "$project.rootDir/settings/java-base.gradle"
    apply from: "$project.rootDir/settings/groovy-base.gradle"
    apply from: "$project.rootDir/settings/publish.gradle"
    apply from: "$project.rootDir/settings/jacoco.gradle"
    apply plugin: 'groovy'
    apply plugin: IntegrationServerPackagePlugin
    configurations {
        nodeutil {
            visible = false
        }
        testImplementation.extendsFrom compileOnly
    }
    repositories {
        ivy {
            name = 'sag-central'
            layout 'maven'
            url = 'http://bas.eur.ad.sag:8081/artifactory/build-snapshot'
        }
    }
    dependencies {
        nodeutil group:'com.softwareag.mom.is', name:'is-server', version:project.sag.is.version, configuration:'nodeutil'
        compileOnly project.sag.module.is.compile
        testImplementation project(path:":modules:$project.rootProject.name-core", configuration:'testFixtureRuntimeElements')
        testRuntimeOnly project.sag.module.is.runtime
    }
    frag {
        nodeUtilClasspath = project.configurations.nodeutil
    }
    task syncPkg(type: Sync, dependsOn: assemble, group: 'IS instance') {
        description = 'Synchronizes the IS package build results to the IS instance directory.'
        project.afterEvaluate { //Need to evaluate sub-projects first to get the archive path right
            into new File( project.sag.is.rootDir, "packages/$project.label")
            from zipTree( project.tasks.pkg.archivePath)
        }
    }
    project.plugins.withType(JavaPlugin) {
        /*
         * Add to the consolidated JaCoCo report
         */
        project.rootProject.tasks.jacocoMerge.dependsOn project.tasks.test
        project.rootProject.tasks.jacocoMerge.executionData files(project.tasks.test.jacoco.destinationFile as File).filter { f -> f.exists() }
        project.rootProject.tasks.jacocoMergeReport.sourceSets project.sourceSets.main
        /*
         * Configure JaCoCo report
         */
        jacocoTestReport {
            reports.xml.enabled true
        }
        project.rootProject.tasks.sonarqube.dependsOn project.tasks.jacocoTestReport
    }
}
/*
 * Additional plugin types
 */
class IntegrationServerPackagePlugin implements Plugin<Project> {
    void apply(Project project) {
        def extension = project.extensions.create('is-pkg', com.softwareag.bas.is.plugins.IntegrationServerPackagePluginExtension, project)
        project.ext.label = ''        
        project.tasks.create('frag', com.softwareag.bas.is.tasks.Frag) {
            from 'src/main/java'
        }
        project.tasks.create('pkg', com.softwareag.bas.is.tasks.bundling.IntegrationServerPackage) {
            dependsOn project.tasks.frag
            duplicatesStrategy org.gradle.api.file.DuplicatesStrategy.FAIL
            from extension.pkgDir
            docFiles extension.docDir
        }
        project.tasks.assemble.dependsOn project.tasks.pkg
    }
}
