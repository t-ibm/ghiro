description = 'Product modules'
/*
 * Project domain specific configuration
 */
subprojects {
    apply from: "$project.rootDir/settings/java-base.gradle"
    apply from: "$project.rootDir/settings/groovy-base.gradle"
    apply from: "$project.rootDir/settings/publish.gradle"
    apply from: "$project.rootDir/settings/jacoco.gradle"
    apply plugin: 'groovy'
    dependencies {
        testImplementation project.sag.module.spock.implementation
        testRuntimeOnly project.sag.module.spock.runtime
    }
    project.plugins.withType(JavaPlugin) {
        /*
         * Add to the consolidated JaCoCo report
         */
        project.rootProject.tasks.jacocoMerge.dependsOn project.tasks.test
        project.rootProject.tasks.jacocoMerge.executionData files(project.tasks.test.jacoco.destinationFile as File).filter { f -> f.exists() }
        project.rootProject.tasks.jacocoMergeReport.sourceSets project.sourceSets.main
        /*
         * Add to the code quality configuration
         */
        sonarqube.properties {
            properties('sonar.coverage.jacoco.xmlReportPaths' : project.rootProject.tasks.jacocoMergeReport.reports.xml.destination)
        }
    }
}
