/*
 * Add test fixture source set etc.
 */
apply from: "$project.rootDir/settings/java-base.gradle"
sourceSets {
    testFixture
    testFixtureTest
}
configurations {
    testFixtureImplementation.extendsFrom implementation
    testFixtureCompileOnly.extendsFrom compileOnly
    testFixtureRuntimeOnly.extendsFrom runtimeOnly
    testImplementation.extendsFrom testFixtureImplementation
    testCompileOnly.extendsFrom testFixtureCompileOnly
    testRuntimeOnly.extendsFrom testFixtureRuntimeOnly
    testFixtureTestImplementation.extendsFrom testFixtureImplementation
    testFixtureTestCompileOnly.extendsFrom testFixtureCompileOnly
    testFixtureTestRuntimeOnly.extendsFrom testFixtureRuntimeOnly
    testFixtureRuntimeElements {
        description = "Runtime elements for source set 'testFixture'."
        extendsFrom testFixtureImplementation, testFixtureRuntimeOnly
    }
}
dependencies {
    testFixtureImplementation(
            project.sag.module.spock.implementation,
            project.sag.module.log.api,
    )
    testFixtureRuntimeOnly(
            project.sag.module.spock.runtime,
            project.sag.module.log.runtime,
    )
    testFixtureImplementation sourceSets.main.output
    testImplementation sourceSets.testFixture.output
    testFixtureTestImplementation sourceSets.testFixture.output
}
task testFixtureTest(type: Test) {
    description = "Testing the 'testFixture' source set."
    testClassesDirs = project.sourceSets.testFixtureTest.output.classesDirs
    classpath = project.sourceSets.testFixtureTest.runtimeClasspath
}
project.tasks.build.dependsOn project.tasks.testFixtureTest
task jarTestFixture(type:Jar, group:'build') {
    description = "Assembles a jar archive containing the $project.sourceSets.testFixture.name classes."
    from sourceSets.testFixture.output
    appendix = 'test'
}
project.tasks.assemble.dependsOn project.tasks.jarTestFixture
artifacts {
    testFixtureImplementation(jarTestFixture) { type = 'test' }
    archives(jarTestFixture) { type = 'test' }
}
/*
 * Add to the consolidated JaCoCo report
 */
apply from: "$project.rootDir/settings/jacoco.gradle"
project.rootProject.tasks.jacocoMerge.dependsOn project.tasks.testFixtureTest
project.rootProject.tasks.jacocoMerge.executionData files(project.tasks.testFixtureTest.jacoco.destinationFile as File).filter { f -> f.exists() }
project.rootProject.tasks.jacocoMergeReport.sourceSets project.sourceSets.testFixture
/*
 * Add to the code quality configuration
 */
def testFixtureSrcDirs = project.sourceSets.testFixture.allSource.srcDirs.findAll { File f -> f.exists() }
def testFixtureTestSrcDirs = project.sourceSets.testFixtureTest.allSource.srcDirs.findAll { File f -> f.exists() }
sonarqube.properties {
    properties(
        'sonar.sources' : properties['sonar.sources'] == null ? testFixtureSrcDirs : properties['sonar.sources'] + testFixtureSrcDirs,
        'sonar.tests' : properties['sonar.tests'] == null ? testFixtureTestSrcDirs : properties['sonar.tests'] + testFixtureTestSrcDirs,
        'sonar.coverage.jacoco.xmlReportPaths' : project.rootProject.tasks.jacocoMergeReport.reports.xml.destination,
    )
}
